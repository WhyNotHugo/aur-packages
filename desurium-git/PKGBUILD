# Maintainer: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jookia <166291@gmail.com>
# Contributor: Matthias "matthiaskrgr" Kr√ºger

pkgname=desurium-git
_pkgname=Desurium
pkgver=1736.ff05d69
pkgrel=1
pkgdesc="Cross-platform game distribution, with an open source client."
url="http://www.desura.com/"
license=('GPL3')
arch=('i686' 'x86_64')
makedepends=('cmake' 'git' 'boost' 'scons' 'gendesk' 'sed')
depends=('v8' 'tinyxml' 'boost-libs' 'flashplugin' 'libxxf86vm' 'gperf'
         'curl' 'c-ares' 'libnotify' 'libgnome-keyring' 'yasm' 'libxslt'
         'cups' 'flex' 'alsa-lib' 'libjpeg-turbo' 'speex' 'libevent'
         'gconf' 'xdg-user-dirs' 'flac' 'libwebp' 'xdg-utils')
provides=('desura')
conflicts=('desura' 'desurium' 'desurium-cef')
source=("git+https://git@github.com/desura/Desurium.git")
md5sums=('SKIP')

if [ "${CARCH}" = "x86_64" ]; then
  depends+=('binutils-multilib')
else
  depends+=('binutils')
fi

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_pkgname"
  if [ ! -d build ] ; then
    mkdir build
  fi
  cd build

  CMAKE_ARGS="
    -DCMAKE_INSTALL_PREFIX=/usr
    -DBINDIR=/usr/bin
    -DDATADIR=/usr/share/
    -DRUNTIME_LIBDIR=/usr/lib/
    -DBUILD_CEF=ON
    -DWITH_FLASH=off
    -DBUILD_TESTS=OFF
    -DH264_SUPPORT=on"
  cmake $CMAKE_ARGS ..
  make
}

#check() {
#  cd "$srcdir/Desurium/build"
#  make test
#}

package() {
  cd "$srcdir/$_pkgname/build"

  make DESTDIR="$pkgdir/" install

  cd $srcdir

  gendesk -f -n ../PKGBUILD
  sed -i \
    -e 's/Icon=.*$/Icon=desura/' \
    -e 's/Exec=.*$/Exec=desura/' \
    -e 's/Categories=.*$/Categories=Game;/' \
    -e 's/Name=.*$/Name=Desurium/' ${pkgname}.desktop

  install -m644 -D \
    ${srcdir}/${_pkgname}/src/branding_desurium/data/desura_lin.png \
    ${pkgdir}/usr/share/pixmaps/desura.png

  install -m644 -D \
    ${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/desura.desktop

  mkdir -p ${pkgdir}/usr/bin
}
