# Maintainer: Hugo Osvaldo Barrera <hugo@barrera.io>


pkgname=lightum-git
_pkgname=lightum
pkgver=latest
pkgrel=1
pkgdesc="MacBook automatic keyboard brightness daemon."
arch=('x86_64' 'i686')
url="https://github.com/poliva/lightum"
license=(GPL)
depends=('dbus-glib' 'libxss' 'libx11' 'libxext' 'xorg-xbacklight')
makedepends=('git')
conflicts=(lightum)
provides=(lightum)
source=("git+http://github.com/poliva/lightum.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd "$srcdir/$_pkgname"
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}
