# Maintainer: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>
# Contributor: Dmitrij D. Czarkoff <czarkoff@gmail.com>
# Contributor: Swift Geek

# Please note you'll need to download the Linux tar.bz2 in order for this
# PKGBUILD to work, since PA is propietary software.
# https://store.uberent.com/download/pa

pkgname=planetary-annihilation
_realver=59607
pkgver=0.${_realver}
_curlver=7.23.0
pkgrel=1
pkgdesc="RTS gameplay in a way that's never been seen before."
arch=("x86_64")
url="http://www.uberent.com/pa/"
license=("custom")
depends=("alsa-lib" "gconf" "nss" "gtk2" "libtxc_dxtn" "rtmpdump")
options=("!libtool" "!strip")
source=(PA_Linux_${_realver}.tar.bz2::
        CoherentUI_Host
        http://curl.haxx.se/download/curl-${_curlver}.tar.gz
        PA.desktop
        PA.png
        content.json
        uifix.js
        PA.sh)
noextract=(PA_Linux_${_realver}.tar.bz2)
PKGEXT=".pkg.tar"

build() {
  cd "$srcdir/curl-${_curlver}"
  ./configure
  make
}

package() {
  # Game itself
  install -d "${pkgdir}/opt/"
  tar xf "${srcdir}/PA_Linux_${_realver}.tar.bz2" -C "${pkgdir}/opt/"
  find "$pkgdir/opt/PA" -type f -exec chmod -R u+w,go-w {} \;
  chown -R root:root "$pkgdir/opt/PA"

  # Patched libcurl
  install -Dm 644 "$srcdir/curl-${_curlver}/lib/.libs/libcurl.so.4" "${pkgdir}/opt/PA"

  # License
  install -Dm 644 "${pkgdir}/opt/PA/licenses.txt" "${pkgdir}/usr/share/licenses/$pkgname/licenses.txt"

  # Link libudev.so.0
  install -d "${pkgdir}"/usr/lib
  ln -s /usr/lib/libudev.so "${pkgdir}/usr/lib/libudev.so.0"

  # Launcher script
  install -d "${pkgdir}"/usr/bin
  install -Dm 755 "${srcdir}/PA.sh" "${pkgdir}/usr/bin/PA"

  # DE Launcher icon
  mkdir -p "$pkgdir/opt" "$pkgdir/usr/share/applications" \
    "$pkgdir/usr/share/icons"
  install -m644 "$srcdir/PA.desktop" "$pkgdir/usr/share/applications"
  install -m644 "$srcdir/PA.png" "$pkgdir/usr/share/icons"

  ### QUIRKS ###
  cd "$pkgdir/opt/PA"

  # Workaround for "black screen" issue
  rm media/shaders/content.json
  install -m644 "$srcdir/content.json" media/shaders

  # mouse cursor
  cat "$srcdir/uifix.js" >> media/ui/alpha/start/start.js

  echo "void wrap(){}" | cc -fPIC -shared -Wl,-soname,libdummyudev.so.0 \
    -o "host/libudev.so.0" -xc -
}


md5sums=('a3aff2c75080fcfab121589e32daea07'
         '115337eb3c98d32e5317b499205955d7'
         '018a9acee77ed70017c6f6cec855635a'
         '16b2acc35fe4cd6fa06aff57b4a6fdc4'
         '463f4e99f44ff6ce9c09e383767a6b1d'
         'd77de6676e68c1c355fe858a0522cae0'
         'f37053bfa07d40911c0c0a4404a54614'
         'c68a1185aaa237a8924201bf98937e95')
