# Maintainer: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>
# Contributor: Simon 'ALSimon' Gilliot <simon@gilliot.fr>

pkgname=transmission-remote-gtk2
_pkgname=transmission-remote-gtk
pkgver=1.1.1
pkgrel=1
pkgdesc="GTK application for remote management of the Transmission BitTorrent client via its RPC interface."
arch=('i686' 'x86_64')
url="http://code.google.com/p/transmission-remote-gtk/"
license=('GPL2')
depends=('json-glib' 'curl' 'libproxy' 'libnotify' 'gtk2' 'geoip' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool')
install="${pkgname}.install"
options=('!libtool')
conflicts=("transmission-remote-gtk")
source=("http://transmission-remote-gtk.googlecode.com/files/${_pkgname}-${pkgver}.tar.gz")
md5sums=('61c44578ad059d8ba4758806805c164e')

build()
{
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-gtk2
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
