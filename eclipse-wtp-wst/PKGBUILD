# Maintainer: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>
# Contributor: Jesus Jerez <jhuss@archlinux.org.ve>

pkgname=eclipse-wtp-wst
pkgver=3.6.0
_pkgbuild=20130925174106
pkgrel=1
pkgdesc="Web Developer Tools for the Eclipse platform - part of webtools, includes enough to do web development (e.g. HTML, CSS, Javascript, and XML)"
url="http://www.eclipse.org/webtools/"
arch=('any')
license=('EPL')
depends=('eclipse-emf' 'eclipse-gef')
makedepends=('unzip')
conflicts=('eclipse-wtp')
provides=('eclipse-wtp')
#changelog=$pkgname.changelog
source=("http://www.eclipse.org/downloads/download.php?r=1&file=/webtools/downloads/drops/R${pkgver}/I-$pkgver-${_pkgbuild}/wtp4x-sdk-I-$pkgver-${_pkgbuild}.zip")
sha256sums=('3d578c39ae1e0a8380b28a778eb13fca0a57cf00a990b743fecfa1ae97e5ce74')

package() {

  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}/eclipse

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}
