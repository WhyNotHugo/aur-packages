# Maintainer: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: LookTJ/Taylor Lookabaugh <jesus.christ.i.love@gmail.com>
# Contributor: alessandro_ufms

pkgname=caffeine-oneclick
pkgver=406
pkgrel=1
pkgdesc="The latest version of caffeine that worked properly AND that only requiered a single click to enable."
arch=(any)
url=https://launchpad.net/caffeine
license=(GPL3)
depends=(dconf gtk2 hicolor-icon-theme kaa-metadata libappindicator3 python2-dbus python2-gobject
    python2-notify python2-xdg python2-xlib)
makedepends=(bzr)
conflicts=("caffeine" "caffeine-bzr")
options=(!emptydirs !libtool)
install=$pkgname.install
source=($pkgname::bzr+http://bazaar.launchpad.net/~caffeine-developers/${pkgname%-*}/main/
    $pkgname-signal.patch)
sha256sums=('SKIP'
    '32c5c65ce98f0e62b37482e999b8fffe8915ffa870bf8f7a4e7adf6bfa0c73d3')

prepare() {
    cd $pkgname/
    bzr revert -r$pkgver
    find -name '*.py' -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
    chmod +x setup.py
    patch -p1 -i ../$pkgname-signal.patch
}

package() {
    cd $pkgname/
    bzr revert -r$pkgver
    chmod u+x setup.py
    python2 setup.py install --root="$pkgdir"
}
