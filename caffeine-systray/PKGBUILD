# Maintainer: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Shimi Chen <shimi.chen@gmail.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: LookTJ/Taylor Lookabaugh <jesus.christ.i.love@gmail.com>
# Contributor: alessandro_ufms
# Contributer: Bluthund <bluthund23@gmail.com>

pkgname=caffeine-systray
pkgver=2.4.1.r543.becf5e9
pkgrel=1
pkgdesc="Latest version supporting systray. Bugfix branch."
arch=(any)
url=https://launchpad.net/caffeine
license=(GPL3)
depends=(dconf gtk2 hicolor-icon-theme kaa-metadata python2-dbus
         python2-gobject python2-notify python2-xdg python2-xlib)
conflicts=(caffeine caffeine-bzr)
provides=(caffeine)
makedepends=(git)
options=(!emptydirs !libtool)
install=$pkgname.install
source=($pkgname::git+https://github.com/hobarrera/taurine.git)
sha256sums=('SKIP')

pkgver() {
    cd "$pkgname"
    echo $(cat VERSION).$(printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)")
}

prepare() {
    cd $pkgname/
    find -name '*.py' -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
    chmod +x setup.py
}

package() {
    cd $pkgname/
    ./setup.py install --root="$pkgdir"
}
