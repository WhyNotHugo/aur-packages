# Maintainer: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Shimi Chen <shimi.chen@gmail.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: LookTJ/Taylor Lookabaugh <jesus.christ.i.love@gmail.com>
# Contributor: alessandro_ufms
# Contributer: Bluthund <bluthund23@gmail.com>

pkgname=caffeine-systray
pkgver=2.4.1.498.7c8910c
pkgrel=4
pkgdesc="Latest version supporting systray rather than appindicator, but lacking any bugfixes since the drop of support. Hopefully a proper fork will be set up soon."
arch=(any)
url=https://launchpad.net/caffeine
license=(GPL3)
depends=(dconf gtk2 hicolor-icon-theme kaa-metadata python2-dbus
         python2-gobject python2-notify python2-xdg python2-xlib)
conflicts=(caffeine caffeine-bzr)
provides=(caffeine)
makedepends=(bzr)
options=(!emptydirs !libtool)
install=$pkgname.install
source=($pkgname::bzr+http://bazaar.launchpad.net/~caffeine-developers/${pkgname%-*}/main/
    $pkgname-signal.patch
    $pkgname-py2fix.patch)
sha256sums=('SKIP'
            '32c5c65ce98f0e62b37482e999b8fffe8915ffa870bf8f7a4e7adf6bfa0c73d3'
            '79bc0e23100d3345f9f1e59cc4739a13c4020efbd7ea3996a7bbba3d9668da20')

pkgver() {
    _commit=498

    cd $pkgname/
    bzr revert -r$_commit
    echo $(cat VERSION).$_commit.$(bzr testament -r$_commit | awk '/sha1/ { print $2 }' | head -c7)
}

prepare() {
    cd $pkgname/
    find -name '*.py' -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
    chmod +x setup.py
    patch -p1 -i ../$pkgname-signal.patch
    patch -p1 -i ../$pkgname-py2fix.patch
}

package() {
    cd $pkgname/
    ./setup.py install --root="$pkgdir"
}
