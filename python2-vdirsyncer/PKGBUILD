# Maintainer: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=python2-vdirsyncer
_pkgname=vdirsyncer
pkgver=0.2.5
pkgrel=1
pkgdesc="Synchronize CalDAV and CardDAV."
arch=('any')
url="https://github.com/untitaker/vdirsyncer"
license=('MIT')
depends=('python2-click' 'python2-setuptools' 'python2-lxml'
         'python2-icalendar' 'python2-requests-toolbelt')
checkdepends=('python2-pytest' 'python2-wsgi-intercept>=0.6.1'
              'python2-radicale')
source=("https://github.com/untitaker/${_pkgname}/archive/${pkgver}.zip"
        "build.patch")
md5sums=('d6649c1f2c8aef4a688fc71906a58cd9'
         'b0b1e52f38d13d2e856a9699ca84410d')


check() {
  cd "$srcdir/${_pkgname}-$pkgver"
  patch build.sh < ../build.patch
  sh build.sh tests
}

build() {
  cd "$srcdir/${_pkgname}-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/${_pkgname}-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

