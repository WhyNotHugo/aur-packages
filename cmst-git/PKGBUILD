# Maintainer: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Andrew Bibb <ajbibb@myfairpoint.net>

pkgname=cmst-git
_pkgname=cmst
pkgver=latest
pkgrel=3
pkgdesc="A QT based GUI front end for the connman connection manager with systemtray icon"
arch=('i686' 'x86_64')
url="https://github.com/andrew-bibb/cmst"
license=('MIT')
provides=('cmst')
conflicts=('cmst')
depends=('qt5-base' 'connman' 'libxkbcommon-x11' 'hicolor-icon-theme')
source=("cmst::git+https://git@github.com/andrew-bibb/cmst.git")
md5sums=(SKIP)
install=${pkgname}.install

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags | sed 's/^cmst-//; s/-/.r/; s/-/./g'
}

build() {
  cd "$srcdir/$_pkgname"
  qmake-qt5 DISTRO=arch
  make 
}


package() {
    cd "$srcdir/$_pkgname"
    make INSTALL_ROOT="$pkgdir/" install

    install -Dm 644 "$srcdir/$_pkgname/text/license.txt" \
      "$pkgdir/usr/share/licenses/cmst-git/LICENSE"
}

