# Maintainer: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=mp4fpsmod-git
_pkgname=mp4fpsmod
pkgver=latest
pkgrel=1
pkgdesc="Simple CLI MP4 time code editor"
arch=('any')
license=('custom')
source=("git+ssh://git@github.com/nu774/${_pkgname}.git")
url="https://sites.google.com/site/qaacpage/junk"
makedepends=('git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/$_pkgname/mp4v2"

  ./make_configure
  ./configure --enable-shared=no --disable-util
}

build() {
  cd "$srcdir/$_pkgname/mp4v2"
  make

  cd "$srcdir/$_pkgname"
  make LIBPATH=-L./mp4v2/.libs

  strip mp4fpsmod
}

package() {
  cd "$srcdir/$_pkgname"

  install -D -m755 mp4fpsmod ${pkgdir}/usr/bin/mp4fpsmod
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
