# Contributor: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>
# Contributor: Loui Chang <louipc dot ist at NOSPAM gmail dot com>

pkgname=mutt-kz
pkgver=1.5.21.5761.785619c
pkgrel=1
pkgdesc="Mutt with notmuch support and another improvements."
url="https://github.com/karelzak/mutt-kz"
arch=(i686 x86_64)
license=('GPL')
depends=('libidn' 'krb5' 'notmuch-runtime')
makedepends=('git' 'gnupg' 'libxslt')
conflicts=('mutt')
provides=('mutt')
options=('!strip')
source=('git+ssh://git@github.com/karelzak/mutt-kz.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  echo 1.5.21.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$pkgname"

  ././prepare
  ./configure --enable-notmuch \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gpgme \
    --enable-pop \
    --enable-imap \
    --enable-smtp \
    --enable-pgp \
    --enable-hcache \
    --with-curses=/usr \
    --with-regex \
    --with-gss=/usr \
    --with-ssl=/usr \
    --with-sasl \
    --with-idn

  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install

  rm "${pkgdir}"/usr/bin/{flea,muttbug}
  rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
  rm "${pkgdir}"/etc/mime.types{,.dist}
  install -Dm644 contrib/gpg.rc "${pkgdir}/etc/Muttrc.gpg.dist"
}
