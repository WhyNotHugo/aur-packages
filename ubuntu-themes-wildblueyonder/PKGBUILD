# Maintainer: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: D. Can Celasun <dcelasun@gmail.com>

pkgname=ubuntu-themes-wildblueyonder
_pkgname=ubuntu-themes
pkgver=14.04+14.04.20140402
pkgrel=1
pkgdesc="Ambiance and Radiance themes, with a slight colour variation."
arch=('any')
url="https://launchpad.net/ubuntu-themes"
license=("CCPL")
depends=("gtk-engine-murrine" "gtk3" "gtk-engine-unico")
makedepends=('python2' 'imagemagick' 'icon-naming-utils')
source=(https://launchpad.net/ubuntu/+archive/primary/+files/${_pkgname}_${pkgver}.orig.tar.gz
        gtk2.patch gtk3.patch)
conflicts=("ubuntu-themes" "ubuntu-mono" "light-themes-wildblueyonder")
provides=("ubuntu-themes" "light-themes-wildblueyonder")
replaces=("light-themes-wildblueyonder")
md5sums=('9f5b751c1a072708ae2d1d3618eb4f01'
         '82ca564f78217c88358fedc5003bf149'
         'b3808ebed9a637971a934646349cde69')

prepare () {
  cd "$srcdir"/${_pkgname}-${pkgver}/

  patch Ambiance/gtk-2.0/gtkrc -i "$srcdir/gtk2.patch"
  patch -Np0 -i "../gtk3.patch"

  sed -i 's|^\(#!.*python$\)|\12|g' scavenge.py
}

build() {
  cd "$srcdir"/${_pkgname}-${pkgver}/
  make
}

package () {

  cd "$srcdir"/${_pkgname}-${pkgver}/

  install -dm755   "${pkgdir}/usr/share/themes/"
  cp -rp "Ambiance" "$pkgdir/usr/share/themes"
  cp -rp "Radiance" "$pkgdir/usr/share/themes"

  sed -i -e "/shadow radius/d" "$pkgdir/usr/share/themes/Ambiance/metacity-1/metacity-theme-1.xml"
  sed -i -e "/shadow radius/d" "$pkgdir/usr/share/themes/Radiance/metacity-1/metacity-theme-1.xml"
  sed -i -e "/padding/d" "$pkgdir/usr/share/themes/Ambiance/metacity-1/metacity-theme-1.xml"
  sed -i -e "/padding/d" "$pkgdir/usr/share/themes/Radiance/metacity-1/metacity-theme-1.xml"

  sed -i -e "/trough/d" "$pkgdir/usr/share/themes/Ambiance/metacity-1/metacity-theme-1.xml"
  sed -i -e "/trough/d" "$pkgdir/usr/share/themes/Radiance/metacity-1/metacity-theme-1.xml"

}
